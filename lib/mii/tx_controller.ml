(*
  Bohdan Purtell
  University of Florida

  Module: Tx_controller
  This module serves as an FSM controller for the transmit path of my Hardcaml
  ethernet MAC.
*)

open! Core
open! Hardcaml
open! Signal
open! Helper_circuits
open! Common_types

let () =
  Stdio.print_endline "=== Imported MAC TX Controller ==="
;;

module I = struct
  type 'a t = {
    clock : 'a;
    reset : 'a;
    en  : 'a;

    start        : 'a;
    fifo_empty   : 'a;
    dis_ready    : 'a;  (* tx_byte_disassembler.ready — 1 when it can accept a new byte *)
    payload_last : 'a;  (* s_axis tlast travelling with the current FIFO byte — marks the final payload byte *)
  } [@@deriving hardcaml]
end

module O = struct
  type 'a t = {
    byte_mux_sel : 'a [@bits 3];
    mac_byte_sel : 'a [@bits 3];
    crc_en       : 'a;
    state        : 'a [@bits 3];
    tx_busy      : 'a;  (* 1 while a frame is in flight (Preamble..Fcs), 0 in Idle *)
    pad          : 'a;  (* 1 while zero-padding a sub-minimum payload — datapath emits 0x00 and the FIFO is not popped *)
  } [@@deriving hardcaml]
end

module I_Regs = struct
  type 'a t = {
    byte_counter : 'a [@bits 11];
    busy         : 'a;
    padding      : 'a;  (* 1 once the datagram's real bytes are exhausted but the 46-byte minimum is not yet met *)
    in_preamble  : 'a;
    in_sfd       : 'a;
    in_payload   : 'a;
    in_fcs       : 'a;
  } [@@deriving hardcaml]
end

module I_Wires = struct
  type 'a t = {
    byte_disassembler_en : 'a;
    crc_en               : 'a;
  } [@@deriving hardcaml]
end

let create
  (scope : Scope.t)
  (i) : (_ O.t)
  =
  let open Always in
  let open Variable in

  let _scope : Scope.t = Scope.sub_scope scope "tx_controller_scope" in

  let clock      = i.I.clock in
  let reset      = i.I.reset in
  let clear      = i.I.reset in
  let _en        = i.I.en in
  let start      = i.I.start in
  let fifo_empty = i.I.fifo_empty -- "fifo_empty" in
  let dis_ready  = i.I.dis_ready  -- "dis_ready"  in

  let rising_edge : Reg_spec.t =
    Reg_spec.create ~clock ~clear ()
  in

  let sm = State_machine.create (module States) ~enable:vdd rising_edge in

  let i_regs = I_Regs.Of_always.reg ~enable:vdd rising_edge in
  I_Regs.Of_always.apply_names ~prefix:"reg_" ~naming_op:(Scope.naming scope) i_regs;

  let i_wires = I_Wires.Of_always.wire Signal.zero in
  I_Wires.Of_always.apply_names ~prefix:"wire_" ~naming_op:(Scope.naming scope) i_wires;

  (* local closure helper functions for counter handling *)
  let rst_counter    = i_regs.byte_counter <--. 0 in
  let increm_counter = i_regs.byte_counter <-- i_regs.byte_counter.value +:. 1 in

  compile [
    i_wires.crc_en <--. 0;
    i_regs.busy <-- i_regs.busy.value;

    sm.switch ~default:[] [
      Idle, [
        when_ (start) [
          when_ (~:fifo_empty) [
            i_regs.busy <--. 1;
            i_regs.padding <--. 0;
            rst_counter;
            sm.set_next Preamble;
          ];
        ];
      ];

      Preamble, [ (* 7 bytes of 0x55 *)
        when_ (dis_ready) [
          if_ (i_regs.byte_counter.value ==:. 6) [
            rst_counter;
            sm.set_next Sfd;
          ] [
            increm_counter;
          ];
        ];
      ];

      Sfd, [ (* 1 byte: 0xD5 *)
        when_ (dis_ready) [
          sm.set_next Dst_mac;
        ];
      ];

      Dst_mac, [ (* 6 bytes *)
        when_ (dis_ready) [
          if_ (i_regs.byte_counter.value ==:. 5) [
            rst_counter;
            sm.set_next Src_mac;
          ] [
            increm_counter;
          ];
        ];
      ];

      Src_mac, [ (* 6 bytes *)
        when_ (dis_ready) [
          if_ (i_regs.byte_counter.value ==:. 5) [
            rst_counter;
            sm.set_next Eth_type;
          ] [
            increm_counter;
          ];
        ];
      ];

      Eth_type, [ (* 2 bytes *)
        when_ (dis_ready) [
          if_ (i_regs.byte_counter.value ==:. 1) [
            rst_counter;
            sm.set_next Payload;
          ] [
            increm_counter;
          ];
        ];
      ];

      (* minimum frame 64 bytes (incl FCS, excl preamble/SFD)
         header: 6+6+2 = 14, FCS: 4 → minimum payload: 64-14-4 = 46 bytes.

         The payload length is now data-driven: it ends on [payload_last]
         (s_axis tlast), not a fixed count. A short datagram (< 46 payload
         bytes) is zero-padded up to the minimum before FCS; a long one runs
         until tlast. byte_counter is the running payload-byte index (0-based),
         so index 45 == the 46th byte == exactly the minimum. *)
      Payload, [
        if_ (i_regs.padding.value) [
          (* real bytes are exhausted (fifo_empty); emit zeros on serializer
             ready only, until the frame reaches the 46-byte minimum *)
          when_ (dis_ready) [
            if_ (i_regs.byte_counter.value ==:. 45) [
              rst_counter;
              i_regs.padding <--. 0;
              i_wires.crc_en <--. 1;
              sm.set_next Fcs;
            ] [
              increm_counter;
            ];
          ];
        ] [
          (* consume a real FIFO byte when the serializer can take it *)
          when_ (~:fifo_empty &: dis_ready) [
            if_ (i.I.payload_last) [
              (* final byte of the datagram *)
              if_ (i_regs.byte_counter.value >=:. 45) [
                (* frame already meets the minimum → straight to FCS *)
                rst_counter;
                i_wires.crc_en <--. 1;
                sm.set_next Fcs;
              ] [
                (* sub-minimum → pad the remainder with zeros *)
                i_regs.padding <--. 1;
                increm_counter;
              ];
            ] [
              increm_counter;
            ];
          ];
        ];
      ];

      Fcs, [ (* 4 bytes *)
        when_ (dis_ready) [
          if_ (i_regs.byte_counter.value ==:. 3) [
            (* last FCS byte — frame complete, drop busy exactly on return to Idle *)
            rst_counter;
            i_regs.busy <--. 0;
            sm.set_next Idle;
          ] [
            i_wires.crc_en <--. 1;
            increm_counter;
          ];
        ];
      ];
    ];
  ];

  {
    byte_mux_sel = sm.current;
    mac_byte_sel = select i_regs.byte_counter.value ~high:2 ~low:0;
    crc_en       = i_wires.crc_en.value;
    state        = sm.current;
    tx_busy      = i_regs.busy.value;
    pad          = i_regs.padding.value;
  }
